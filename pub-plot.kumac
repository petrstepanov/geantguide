* conf-plot.kumac.  Uses plot.kumac, babar.kumac and a user-supplied xxx.kumac
* to create an official looking figure xxx.eps.

* usage:  exec conf-plot     xxx        1.      1.     68.   85.
*                         user-kumac  xscale  yscale xbabar ybabar
*
* xscale = yscale = 1. are defaults; produces 15cm square plot.
* xbabar = 68. ybabar = 85. are defaults; location of "BABAR" in 100x100 box.

 macro CONF-PLOT 1=xxx 2=1. 3=1. 4=68. 5=85.

* define sizes and fonts
 exec pub-plot#plot [2] [3]

* now invoke user kumac to actually plot the plot.  May override sizes etc.
 mess now calling [1].kumac
 exec [1].kumac
 
* write BaBar on the upper right corner.  2nd and 3rd arguments are locations
* in 100 x 100 box.
* exec conf-plot#babar [2] [4] [5]

 return

************************************************************************

 macro PLOT 1=1. 2=1. 

* kumac template for authoritative BaBar plots
* initiator : Gautier Hamel de Monchenault

* first  argument is the scale in x, in units of 15 cm
* second argument is the scale in y, in units of 15 cm

* use also the babar.kumac to write BaBar on the upper right corner
   
* pawfigure.kumac is an example of how to use these two kumacs
   
 set * ; opt *

* turn on/off fit results printing

 xxsize = [1]*15
 yysize = [2]*15  

 opt utit
 opt nbox
 opt zfl1
 opt nstat
 opt tic

* Figure size.
 size [xxsize] [yysize]
 
* Number of divisions
 set ndvx 505
 set ndvy 505
 set ndvz 505

* Line widths
 igset lwid 3
 set bwid 2
 set pwid 2
 set hwid 5
 set fwid 5
 
* Fonts
 set tfon -132                  | title font        Times Roman
 set vfon -132                  | axis values font  Times Roman
 set lfon -132                  | axis label font   Times Roman
 set cfon -132                  | key font          Times Roman
 igset txfp -132                | ITX font          Times Roman

* Font sizes
 set tsiz 0.5*[1]               | title size
 set vsiz 0.4*[1]               | axis values size
 set asiz 0.5*[1]               | axis label size
 set csiz 0.4*[1]               | key label size
 igset chhe 0.4*[1]             | character size for ITX

* Margins
* set xmgl 3.*[1] 
* set xmgr 0.4*[1]
* set ymgl 3.*[1]
* set ymgu 0.4*[1]
 
* tics
 set xtic 0.3*[1]
 set ytic 0.3*[1]
 
* Label positions
 set yval 0.4*[1]
 set ylab 1.2*[1]               | x axis label y position
 set xval 0.3*[1]
 set xlab 1.2*[1]               | y axis label x position
 set yhti 1.6*[1]               | title position

* disable  histogram titles by default
 opt utit

* Plot symbol and size
 set mtyp 20
 set errx .01
 set ksiz .10*[1]               | symbol size

 return

***********************************************************************

 macro BABAR [1]=1. [2]=68. [3]=85.

* originator : Gautier Hamel de Monchenault
* modified for Times-BoldItalic: Paul Dauncey 23/06/00

* first argument is the scale factor
* second and third arguments are coordinates in the 100x100 box

 null 0 100 0 100 'ABS'
 igset txfp -32       | Times-BoldItalic

 igset chhe 0.8*[1]
 itx [2]    [3] 'B'
 igset chhe 0.6*[1]
 itx [2]+6  [3] 'A'
 igset chhe 0.8*[1]
 itx [2]+11 [3] 'B'
 igset chhe 0.6*[1]
 itx [2]+17 [3] 'A'
 igset chhe 0.6*[1]
 itx [2]+22 [3] 'R'

* Try to reset the character settings

 igset txfp -132      | Times-Roman
 set chhe 0.4*[1]
 
 return


***********************************************************************

 macro PRELIMINARY [1]=.5 [2]=58. [3]=79.

* originator : Gautier Hamel de Monchenault
* modified for Times-BoldItalic: Paul Dauncey 23/06/00
* modified from BABAR to PRELIMINARY: Jenny Williams 06/02/04
* JCW put prelim text to Times-Italic

* first argument is the scale factor
* second and third arguments are coordinates in the 100x100 box

 null 0 100 0 100 'ABS'
* igset txfp -32       | Times-BoldItalic
 igset txfp -12       | Times-Italic

 igset chhe 0.6*[1]
 itx [2]    [3] 'P'
 igset chhe 0.6*[1]
 itx [2]+4  [3] 'R'
 igset chhe 0.6*[1]
 itx [2]+8 [3] 'E'
 igset chhe 0.6*[1]
 itx [2]+12 [3] 'L'
 igset chhe 0.6*[1]
 itx [2]+15 [3] 'I'
 igset chhe 0.6*[1]
 itx [2]+18 [3] 'M'
 igset chhe 0.6*[1]
 itx [2]+23 [3] 'I'
 igset chhe 0.6*[1]
 itx [2]+26 [3] 'N'
 igset chhe 0.6*[1]
 itx [2]+30 [3] 'A'
 igset chhe 0.6*[1]
 itx [2]+34 [3] 'R'
 igset chhe 0.6*[1]
 itx [2]+38 [3] 'Y'

* Try to reset the character settings

 igset txfp -132      | Times-Roman
 igset chhe 0.4*[1]
 
 return

